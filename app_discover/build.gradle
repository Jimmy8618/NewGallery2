apply plugin: 'com.android.application'
apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.7.9"
}
android {
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    allprojects {
        configurations {
            all {
                resolutionStrategy {
                    force "org.ow2.asm:asm:7.0"
                }
            }
        }
    }
    signingConfigs {
        debug {
            storeFile file('../platform.keystore')
            storePassword 'android'
            keyAlias = 'platform'
            keyPassword 'android'
        }
    }
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId rootProject.app_package_name
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.apk_version_int
        versionName rootProject.apk_version
        flavorDimensions "versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled = true
        }
    }
    buildTypes.each {
        it.buildConfigField "String", "apk_version", "\"" + rootProject.apk_version + "\""
        it.buildConfigField "String", "so_blur_version", "\"" + rootProject.so_blur_version + "\""
        it.buildConfigField "String", "so_bokeh_version", "\"" + rootProject.so_bokeh_version + "\""
        it.buildConfigField "String", "so_dehaze_version", "\"" + rootProject.so_dehaze_version + "\""
        it.buildConfigField "String", "so_eglfence_version", "\"" + rootProject.so_eglfence_version + "\""
        it.buildConfigField "String", "so_facedetector_version", "\"" + rootProject.so_facedetector_version + "\""
        it.buildConfigField "String", "so_filtershow_version", "\"" + rootProject.so_filtershow_version + "\""
        it.buildConfigField "String", "so_imageblendings_version", "\"" + rootProject.so_imageblendings_version + "\""
        it.buildConfigField "String", "so_jpegstream_version", "\"" + rootProject.so_jpegstream_version + "\""
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    useLibrary 'org.apache.http.legacy'
    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['../src']
            resources.srcDirs = ['../src']
            aidl.srcDirs = ['../src']
            renderscript.srcDirs = ['../src']
            res.srcDirs = ['src/main/res', '../res']
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "NewGallery2.apk"
        }
    }
}
def coverageSourceDirs = [
        'src/main/java',
        '../src'
]

task jacocoTestReport(type: JacocoReport) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: 'build/intermediates/javac/debug/compileDebugJavaWithJavac/classes',
            excludes: ['**/R*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    sourceDirectories = files(coverageSourceDirs)
    executionData = files("$buildDir/outputs/code_coverage/debugAndroidTest/connected/coverage.ec")

    doFirst {
        new File("$buildDir/intermediates/javac/debug/compileDebugJavaWithJavac/classes").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation "junit:junit:$rootProject.junit_junit_version"
    testImplementation "org.robolectric:robolectric:$rootProject.robolectric_version"
    androidTestImplementation "androidx.test:runner:$rootProject.test_runner_version"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$rootProject.test_uiautomator_version"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.test_ext_junit_version"
    androidTestImplementation "androidx.test:core:$rootProject.test_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.test_espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-remote:$rootProject.test_espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.test_espresso_version"
    implementation "com.google.android.material:material:$rootProject.material_version"
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat_version"
    implementation "androidx.recyclerview:recyclerview:$rootProject.recyclerview_version"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.support_v4_version"
    implementation "androidx.legacy:legacy-support-v13:$rootProject.support_v13_version"
    implementation "androidx.exifinterface:exifinterface:$rootProject.exifinterface"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.glide_version"
    implementation "org.greenrobot:eventbus:$rootProject.eventbus_version"
    implementation "org.jacoco:org.jacoco.core:$rootProject.jacoco_core_version"
    //gallery common
    implementation project(path: ':module_gallerycommon')
    //framework
    implementation files('../libraries/sprd_common_framework.jar')
    implementation files('../libraries/glide-4.10.0.jar')
    //lib
    implementation project(':module_bokeh')
    implementation project(':module_dehaze')
    implementation project(':module_eglfence')
    implementation project(':module_facedetector')
    implementation project(':module_filtershow')
    implementation project(':module_imageblendings')
    implementation project(':module_jpegstream')
    implementation project(':module_smarterase')
}

